plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '4.0.0' apply false
}

wrapper {
    gradleVersion = '8.10.1'
}

description = 'Dropwizard guicey examples'

ext {
    guiceyBom = '8.0.0-rc.2-SNAPSHOT'
    dwVersion = '5.0.0-alpha.5'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'project-report'

    java {
        sourceCompatibility = 17
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories { mavenCentral(); mavenLocal();
        // for github package (used on CI)
        maven {
            url  = 'https://maven.pkg.github.com/xvik/dropwizard-guicey'
            credentials {
                username = findProperty('gpr.user') ?: System.getenv("USERNAME")
                password = findProperty('gpr.key') ?: System.getenv("TOKEN")
            }
        }
    }
    dependencies {
        implementation platform("ru.vyarus.guicey:guicey-bom:$guiceyBom")

        constraints {
            implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:6.0.1'

            implementation 'org.flywaydb:flyway-core:10.18.0'
            // flyway not compatible with h2 1.4.200 anymore
            implementation 'com.h2database:h2:2.3.232'
        }

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'ru.vyarus:spock-junit5:1.2.0'
        testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
        testImplementation "io.dropwizard:dropwizard-testing"
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
        maxHeapSize = "512m"
    }

}

dependencyUpdates.revision = 'release'
